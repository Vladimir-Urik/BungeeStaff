import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

archivesBaseName = 'original-BungeeStaff'
group 'bungeestaff.bungee'
sourceCompatibility = '8'
version '3.1.2'

def pluginName = 'BungeeStaff'
def mainClass = project.property("group") + "." + pluginName + "Plugin"

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = 'https://ci.emc.gs/nexus/content/groups/aikar/'
    }
}

shadowJar {
    archiveBaseName.set(pluginName)
    archiveClassifier.set('')
    archiveVersion.set(project.property("version").toString())
    relocate('com.rabbitmq', project.property("group").toString() + "lib.rabbit")
    relocate('com.zaxxer', project.property("group").toString() + "lib.hikaricp")
    relocate('org.slf4j', project.property("group").toString() + "lib.slf4j")
}

processResources {
    filter ReplaceTokens, tokens: ["version": version, "name": pluginName, "main_class": mainClass]
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compile 'org.jetbrains:annotations:20.1.0'

    compile 'com.rabbitmq:amqp-client:5.10.0'

    compile 'com.zaxxer:HikariCP:4.0.1'

    compileOnly 'net.md-5:bungeecord-api:1.16-R0.4'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
}